# This is a modification of a (now outdated) copy of PhotoconductorSimple.in, modified to generate
# and simulate a Diode device. Tread with caution.

# Experimental Variables

# Try 1e-7, 1e-5, 1e-3.
set lifetime = 1e-7
# Try 1e-4, 1e-1, 1e2, 1e5.
# See if area of pulse is proportional to this.
set ehp_density = 1e-4
# Find integral for all structures (12 PCs, 4 Diodes).

# File Paths

set exp_name = "Diode_lifetime${lifetime}_ehpdensity${ehp_density}"
set file_str = "Diode.str"
set file_time_log = "${exp_name}_time.log"

# Athena

go athena


line x loc=0 spac=0.5
line x loc=1 spac=0.5
line y loc=0.00 spac=0.1
line y loc=100 spac=0.1

init silicon c.phosphor=1e12 orientation=100 two.d

implant boron dose=1.0e15 energy=10 tilt=0 rotation=0 amorph

deposit aluminum thick=1 division=5

structure flip.y
implant phosphor dose=1.0e15 energy=10 tilt=0 rotation=0 amorph
deposit aluminum thick=1 division=5
structure flip.y

electrode name=cathode x=0.5 y=-0.1
electrode name=substrate x=0.5 y=100.1

struct outf=$file_str

# Atlas

go atlas

mesh inf=$file_str

contact name=substrate neutral
contact name=cathode neutral
material region=1 taun0=$lifetime taup0=$lifetime
models bipolar
# TODO: Is this line needed? In testing, it doesn't seem to be.
method newton carriers=2 trap itlimit=20 maxtraps=10 dt.max=100e-9

set time_event_start = 1e-6
singleeventupset entry="0,50" exit="1,50" pcunits b.density=$ehp_density \
                  radialgauss radius=1 t0=$time_event_start tc=0

solve init

output charge

solve vsubstrate=0 vstep=1 vfinal=10 name=substrate

log outf=$file_time_log

# Simulate the pedestal of the current, with large time-steps.
method newton carriers=2 trap itlimit=20 maxtraps=10 dt.max=100e-9
solve tfinal=$time_event_start tstep=1e-9
# Simulate the development of the pulse. Start with pulse_length = 1000-9 (1e-6), and then fit it
# to the size of the pulse, using the plot. dt.max is adjusted so that there are ~50 points.
#
# The values I found to work are:
#   $ehp_density | $pulse_length
#   =============|===========
#   1e-4         | 15e-9
#   1e-1         | 100e-9
#   1e2          | 700e-9
#   1e5          | 740e-9
# These seem to work even with different lifetimes.
set pulse_length = 40e-9
set time_pulse_end = $time_event_start + $pulse_length
method newton carriers=2 trap itlimit=20 maxtraps=10 dt.max=$pulse_length/50
solve tfinal=$time_pulse_end tstep=1e-9
# Simualte the rest of the pedestal. Set tfinal to twice the above tfinal, and dt.max large.
method newton carriers=2 trap itlimit=20 maxtraps=10 dt.max=100e-9
solve tfinal=2*$time_pulse_end tstep=1e-9

system rundll32 user32.dll,MessageBeep

quit
